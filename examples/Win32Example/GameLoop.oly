module GameLoop

open System
open System.Diagnostics

private alias Array = OlyPrelude.Array

ConvertSecondsToTicks(seconds: float64): int64 = TimeSpan.FromSeconds(seconds).Ticks
ConvertTicksToSeconds(ticks: int64): float64 = TimeSpan.FromTicks(ticks).TotalSeconds
ConvertFramesPerSecondToSeconds(fps: float64): float64 = (1000 / fps) * 0.001
ConvertFramesPerSecondToTicks(fps: float64): int64 = ConvertSecondsToTicks(ConvertFramesPerSecondToSeconds(fps))

ConvertTicksToTickRate(ticks: int64): int64 =
    int64((float64(1000) / TimeSpan.FromTicks(ticks).TotalMilliseconds))

#[inline] 
Start(#[inline] preUpdate: float64 -> (), #[inline] fixedUpdate: float64 -> bool, #[inline] update: float64 -> bool, #[inline] render: (frameTime: TimeSpan, float64) -> ()): () =
    let tickRate: uint8 = 30

    let mutable quit = false
    let stopwatch = Stopwatch.StartNew()
    let getTicks() = stopwatch.Elapsed.Ticks

    let skip = ConvertFramesPerSecondToTicks(1)

    let dtFixed = ConvertFramesPerSecondToTicks(float64(tickRate))

    let mutable currentTime = getTicks()
    let mutable accumulator = 0: int64

    while (!quit)
        let newTime = getTicks()
        let frameTime = newTime - currentTime
        currentTime <- newTime

        // pre-update
        preUpdate(ConvertTicksToSeconds(frameTime))

        // fixed-update
        accumulator <- 
            if (frameTime > (dtFixed * 2))
                dtFixed
            else
                accumulator + frameTime
        while (accumulator >= dtFixed)
            quit <- fixedUpdate(ConvertTicksToSeconds(dtFixed))
            accumulator <- accumulator - dtFixed

        // update
        quit <- update(ConvertTicksToSeconds(frameTime))

        // render
        let alpha = System.Math.Clamp(float64(accumulator) / float64(dtFixed), 0, 1)
        render(TimeSpan.FromTicks(frameTime), alpha)