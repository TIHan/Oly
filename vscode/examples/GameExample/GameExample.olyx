#target "dotnet: net7"

#load "*.oly"

#reference "../Evergreen/src/Client/Evergreen.Client.Graphics.olyx"
#package "Veldrid,4.9.0" // TODO: Remove this when we fix transitive packages.
#package "Veldrid.StartupUtilities,4.9.0" // TODO: Remove this when we fix transitive packages.
#package "Veldrid.SPIRV,1.0.15" // TODO: Remove this when we fix transitive packages.

open System.Text
open System.Numerics
open Evergreen.Client.Graphics

DefaultVertexCode: string = "#version 450

layout(location = 0) in vec3 Position;
layout(location = 1) in mat4 Transform;

layout(location = 0) out vec4 fsin_Color;

layout(set = 0, binding = 0) uniform _ModelViewProjection
{
    mat4 ModelViewProjection;
};

void main()
{
    gl_Position = ModelViewProjection * Transform * vec4(Position, 1);
    fsin_Color = vec4(Position.x, Position.y, 1, 1);
}"

DefaultFragmentCode: string = "#version 450

layout(location = 0) in vec4 fsin_Color;
layout(location = 0) out vec4 fsout_Color;

void main()
{
    fsout_Color = fsin_Color;
}"

main(): () =
    let window = Graphics.CreateWindow(100, 100, 1280, 720, "Evergreen Engine")

    print("Graphics Device: " + Graphics.DeviceName + "\n")

    let meshDesc   = MeshDescription.CreateHexagon()
    let shaderDesc = 
        ShaderDescription.CreateMeshShader(
            Encoding.UTF8.GetBytes(DefaultVertexCode), 
            Encoding.UTF8.GetBytes(DefaultFragmentCode)
        )

    let mesh   = Graphics.CreateMesh(meshDesc)
    let shader = Graphics.CreateShader(shaderDesc)
    let shader2 = Graphics.CreateShader(shaderDesc)

    let hexagons = System.Collections.Generic.List<Matrix4x4>()

    let mutable i = 1
    while (i <= 3)
        hexagons.Add(Matrix4x4.CreateTranslation(Vector3(-float32(i * 2), 0, 0)))
        i <- i + 1
    let hexagons1 = hexagons.ToArray()

    hexagons.Clear()
    let mutable i = 1
    while (i <= 3)
        hexagons.Add(Matrix4x4.CreateTranslation(Vector3(float32(i * 2), 0, 0)))
        i <- i + 1
    let hexagons2 = hexagons.ToArray()

    let instances1 = Graphics.CreateInstances(hexagons1)
    let drawCmd1   = DrawCommand.CreateDrawMesh(mesh, shader, instances1)

    let instances2 = Graphics.CreateInstances(hexagons2)
    let drawCmd2   = DrawCommand.CreateDrawMesh(mesh, shader2, instances2)

    let drawCmds = [drawCmd1;drawCmd2]

    let mutable projection = Matrix4x4.CreatePerspectiveFieldOfView((90: float32) * 0.017453, (1280.0: float32) / 720, 0.1, 100)
    let mutable view = Matrix4x4.CreateLookAt(Vector3(0, 0, 5), Vector3.Zero, Vector3(0, 1, 0))
    Graphics.SetModelViewProjection(view * projection)

    GameLoop.Start(
        30,
        deltaTime ->
            let inputState = window.GetInputState()
            !window.Exists,
        deltaTime ->
            !window.Exists,
        (deltaTime, delta) ->
            let windowExists = window.Exists
            if (windowExists)
                Graphics.Draw(drawCmds)
            !windowExists
    )

    Graphics.DestroyWindow()
