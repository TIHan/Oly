// http://gafferongames.com/game-physics/fix-your-timestep/
module GameLoop

open System
open System.Diagnostics

#[inline] 
Start(fixedUpdateIntervalInFramesPerSecond: float64, #[inline] update: () -> (), #[inline] fixedUpdate: (t: float64, dt: float64) -> bool, #[inline] render: float64 -> ()): () =
    let mutable quit = false
    let stopwatch = Stopwatch.StartNew()

    let mutable t = 0.0
    let dt = (1000 / fixedUpdateIntervalInFramesPerSecond) * 0.001

    let mutable currentTime = stopwatch.Elapsed.TotalSeconds
    let mutable accumulator = 0.0

    while (!quit)
        let newTime = stopwatch.Elapsed.TotalSeconds
        let mutable frameTime = newTime - currentTime
        if (frameTime > 0.25)
            frameTime <- 0.25
        currentTime <- newTime
        accumulator <- accumulator + frameTime

        update()

        while (accumulator >= dt)
            quit <- fixedUpdate(t, dt)
            t <- t + dt
            accumulator <- accumulator - dt

        let alpha = accumulator / dt          
        render(alpha)
        