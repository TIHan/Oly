module GameLoop

open System
open System.Diagnostics

private ConvertSecondsToTicks(seconds: float64): int64 = TimeSpan.FromSeconds(seconds).Ticks
private ConvertTicksToSeconds(ticks: int64): float64 = TimeSpan.FromTicks(ticks).TotalSeconds
private ConvertFramesPerSecondToSeconds(fps: float64): float64 = (1000 / fps) * 0.001
private ConvertFramesPerSecondToTicks(fps: float64): int64 = ConvertSecondsToTicks(ConvertFramesPerSecondToSeconds(fps))

private ConvertTicksToTickRate(ticks: int64): int64 =
    int64((float64(1000) / TimeSpan.FromTicks(ticks).TotalMilliseconds))

#[inline] 
Start(#[inline] preUpdate: float64 -> (), #[inline] fixedUpdate: float64 -> bool, #[inline] update: float64 -> bool, #[inline] render: (float64, float64) -> ()): () =
    let tickRate: uint8 = 30
    let maxRenderTickRate: uint8 = 120

    let mutable quit = false
    let stopwatch = Stopwatch.StartNew()
    let getTicks() = stopwatch.Elapsed.Ticks

    let skip = ConvertFramesPerSecondToTicks(1)

    let dtFixed = ConvertFramesPerSecondToTicks(float64(tickRate))
    let dtMaxRender = ConvertFramesPerSecondToTicks(float64(maxRenderTickRate))

    let mutable dtRender = dtMaxRender
    let mutable currentTime = getTicks()
    let mutable accumulator = 0: int64
    let mutable lastRenderTime = currentTime
    let mutable fixedUpdateCount = 0

    let avgCount = int32(tickRate)
    let mutable frameCount = 0
    let mutable frameTimes = Array.ZeroCreate<int64>(avgCount)
    let getAvgFrameTime() =
        let mutable totalFrameTime = 0: int64
        let mutable i = 0
        while (i < frameTimes.Length)
            totalFrameTime <- totalFrameTime + frameTimes[((frameCount + i) % avgCount)]
            i <- i + 1
        int64(float64(totalFrameTime) / float64(avgCount))

    let check(frameTime) =
        if (fixedUpdateCount > 0)
            frameTimes[frameCount % avgCount] <- frameTime
            frameCount <- frameCount + 1

    let mutable renderAccumulator = 0: int64

    while (!quit)
        let newTime = getTicks()
        let frameTime = newTime - currentTime
        currentTime <- newTime

        check(frameTime)
        dtRender <- System.Math.Max(dtMaxRender, getAvgFrameTime())

        renderAccumulator <- renderAccumulator + frameTime

        // pre-update
        preUpdate(ConvertTicksToSeconds(frameTime))

        // fixed-update
        accumulator <- 
            if (frameTime > dtFixed)
                dtFixed
            else
                accumulator + frameTime
        fixedUpdateCount <- 0
        while (accumulator >= dtFixed)
          //  renderAccumulator <- int64(float64(dtRender) / 2)
            quit <- fixedUpdate(ConvertTicksToSeconds(dtFixed))
            accumulator <- accumulator - dtFixed
            fixedUpdateCount <- fixedUpdateCount + 1

        // update
        quit <- update(ConvertTicksToSeconds(frameTime))

        if (renderAccumulator > (dtRender * 2))
            renderAccumulator <- dtRender

        // render
        let renderFrameTime = currentTime - lastRenderTime
        if (renderAccumulator >= dtRender)
            renderAccumulator <- renderAccumulator - dtRender
            lastRenderTime <- currentTime
            let alpha = System.Math.Clamp(float64(accumulator) / float64(dtFixed), 0, 1)
            render(ConvertTicksToSeconds(renderFrameTime), alpha)