#target "dotnet: net7"

#load "*.oly"

#reference "../core/prelude.olyx"
#reference "../Evergreen/src/Graphics/Evergreen.Graphics.olyx"
#reference "../Evergreen/src/Window/Evergreen.Window.olyx"

open System
open System.Numerics
open System.Runtime.InteropServices
open Evergreen.Window

private alias vk = Evergreen.Graphics.Backend.Vulkan
private alias VkBufferUsageFlags = TerraFX.Interop.Vulkan.VkBufferUsageFlags
private alias VmaAllocationCreateFlags = Evergreen.Graphics.Backend.Interop.VulkanMemoryAllocator.VmaAllocationCreateFlags

#[intrinsic("bitwise_or")]
(|)(VkBufferUsageFlags, VkBufferUsageFlags): VkBufferUsageFlags
#[intrinsic("bitwise_or")]
(|)(VmaAllocationCreateFlags, VmaAllocationCreateFlags): VmaAllocationCreateFlags

#[StructLayout(LayoutKind.Sequential)]
struct Vertex =
    Position: Vector2
    Color: Vector3

    new(position: Vector2, color: Vector3) =
        {
            Position = position
            Color = color
        }

    overrides ToString(): string = 
        "(Position = " + this.Position.ToString() + ", Color = " + this.Color.ToString() + ")"

Vertices: Vertex[] =
    [
        Vertex(Vector2(0, -0.5), Vector3(1, 0, 0));
        Vertex(Vector2(0.5, 0.5), Vector3(0, 1, 0));
        Vertex(Vector2(-0.5, 0.5), Vector3(0, 0, 1))
    ]

main(): () =
    let window = IWindow.CreateWin32("Vulkan Example")

    let vulkanAppOptions = vk.VulkanApplicationOptions()
    vulkanAppOptions.ValidationEnabled <- true
    let vulkanApp = vk.Initialize(window.HWND, window.HINSTANCE, vulkanAppOptions)

    printLine(vulkanApp.GetDeviceName())

    let buffer = 
        vk.CreateBuffer(
            vulkanApp, 
            65536, 
            VkBufferUsageFlags.VK_BUFFER_USAGE_VERTEX_BUFFER_BIT | VkBufferUsageFlags.VK_BUFFER_USAGE_TRANSFER_DST_BIT,
            VmaAllocationCreateFlags.VMA_ALLOCATION_CREATE_DEDICATED_MEMORY_BIT
           // VmaAllocationCreateFlags.VMA_ALLOCATION_CREATE_HOST_ACCESS_SEQUENTIAL_WRITE_BIT | VmaAllocationCreateFlags.VMA_ALLOCATION_CREATE_MAPPED_BIT
        )
    vk.UpdateBuffer(vulkanApp, buffer, Vertices)

    let vertexShaderByteCode = System.IO.File.ReadAllBytes("Win32Example/vert.spv")
    let fragmentShaderByteCode = System.IO.File.ReadAllBytes("Win32Example/frag.spv")

    let vertexShaderModule = vk.CreateShaderModule(vulkanApp, ReadOnlySpan(vertexShaderByteCode))
    let fragmentShaderModule = vk.CreateShaderModule(vulkanApp, ReadOnlySpan(fragmentShaderByteCode))

    let pipelineLayout = vk.CreatePipelineLayout(vulkanApp)

    let recordCommands(commandBuffer: vk.VulkanCommandBuffer, graphicsPipeline, renderPass, framebuffer, extent, viewport, scissor) =
        vk.ResetCommandBuffer(vulkanApp, commandBuffer)

        commandBuffer.Begin()
        commandBuffer.BeginRenderPass(renderPass, framebuffer, extent)

        commandBuffer.BindPipeline(graphicsPipeline, vk.VkPipelineBindPoint.VK_PIPELINE_BIND_POINT_GRAPHICS)
        commandBuffer.SetViewport(viewport)
        commandBuffer.SetScissor(scissor)
        commandBuffer.BindVertexBuffer(buffer)
        commandBuffer.Draw(3, 1)

        commandBuffer.EndRenderPass()
        commandBuffer.End()

    let commandBuffers =
        let commandBuffers = Array.ZeroCreate(vulkanApp.MaxFramesInFlight)
        let mutable i = 0
        while (i < commandBuffers.Length)
            commandBuffers[i] <- vk.CreateGraphicsCommandBuffer(vulkanApp)
            i <- i + 1
        commandBuffers

    let mutable swapChain = unchecked default
    let mutable graphicsPipeline = unchecked default
    let mutable renderPass = unchecked default
    let mutable swapChainFramebuffers = unchecked default
    let recreateSwapChain() =
        vk.ResetCurrentFrame(vulkanApp)
        swapChain <- vk.CreateSwapChain(vulkanApp)
        renderPass <- vk.CreateRenderPass(vulkanApp, swapChain)
        swapChainFramebuffers <- vk.CreateSwapChainFramebuffers(vulkanApp, swapChain, renderPass)

        let bindingDescs = [vk.CreateVertexBindingDescription<Vertex>(0, false)]
        let attrDescs = vk.CreateVertexAttributeDescriptions<Vertex>(0)
        graphicsPipeline <- vk.CreateGraphicsPipeline(vulkanApp, swapChain, bindingDescs, attrDescs, pipelineLayout, renderPass, vertexShaderModule, fragmentShaderModule)

    let cleanupSwapChain() =
        if (swapChain !== unchecked default)
            vk.DestroyPipeline(vulkanApp, graphicsPipeline)
            vk.DestroyFramebuffers(vulkanApp, ReadOnlySpan(swapChainFramebuffers))   
            vk.DestroyRenderPass(vulkanApp, renderPass)
            vk.DestroySwapChain(vulkanApp, swapChain)
            swapChain <- unchecked default

    recreateSwapChain()
    window.Show()

    let draw() =
        try
            vk.Draw(vulkanApp, swapChain, 
                (imageIndex, frame) -> 
                    let commandBuffer = commandBuffers[frame]
                    let framebuffer = swapChainFramebuffers[imageIndex]

                    recordCommands(commandBuffer, graphicsPipeline, renderPass, framebuffer, swapChain.Extent, swapChain.Viewport, swapChain.Scissor)
                    commandBuffer
            )
        catch (ex: vk.VulkanSwapChainOutOfDateException) =>
            vk.WaitForIdle(vulkanApp)
            cleanupSwapChain()
            try
                recreateSwapChain()
            catch (ex: vk.VulkanSwapChainCreationFailedException) =>
                // swap-chain failed to create, bail out of application
                ()
    draw()

    // --- BEGIN LOOP

    while (!window.IsClosed)
        let inputSnapshot = window.PumpEvents()
        let keyEvents = inputSnapshot.KeyEvents

        let mutable i = 0
        while (i < keyEvents.Length)
            let keyEvent = keyEvents[i]
            if (keyEvent.IsDown && !keyEvent.IsRepeat)
                printLine(keyEvent)
            i <- i + 1

        draw()

    // --- END LOOP

    vk.WaitForIdle(vulkanApp)

    vk.DestroyBuffer(vulkanApp, buffer)

    cleanupSwapChain()
    
    vk.DestroyPipelineLayout(vulkanApp, pipelineLayout)

    vk.DestroyShaderModule(vulkanApp, vertexShaderModule)
    vk.DestroyShaderModule(vulkanApp, fragmentShaderModule)

    vk.Destroy(vulkanApp)
